apply plugin: 'com.github.ben-manes.versions'

buildscript {
  ext.versions = [
      'minSdk': 9,
      'compileSdk': 26,
      'buildTools': '26.0.1',

      'androidPlugin': '3.0.0-beta6',
      'androidTools': '25.3.0',

      'butterknife': '8.8.1',
  ]

  ext.deps = [
      androidPlugin: "com.android.tools.build:gradle:${versions.androidPlugin}",
      'butterknife': [
          'runtime': "com.jakewharton:butterknife:${versions.butterknife}",
          'compiler': "com.jakewharton:butterknife-compiler:${versions.butterknife}"
      ],
      'lint': [
          'core': "com.android.tools.lint:lint:${versions.androidTools}",
          'api': "com.android.tools.lint:lint-api:${versions.androidTools}",
          'checks': "com.android.tools.lint:lint-checks:${versions.androidTools}",
          'tests': "com.android.tools.lint:lint-tests:${versions.androidTools}",
      ],
      annotations: 'org.jetbrains:annotations:15.0',

      junit      : 'junit:junit:4.12',
      festassert : 'org.easytesting:fest-assert-core:2.0M10',
      festandroid: 'com.squareup:fest-android:1.0.8',
      mockitocore: 'org.mockito:mockito-core:1.9.5',
      robolectric: 'org.robolectric:robolectric:3.4.2',
  ]

  repositories {
    jcenter()
    google()
    maven {
      url 'https://plugins.gradle.org/m2/'
    }
  }
  dependencies {
    classpath deps.androidPlugin
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.15.0'
    classpath 'gradle.plugin.com.kageiit:lintrules:1.1.3'
  }
}

allprojects {
  repositories {
    mavenCentral()
    jcenter()
    google()
  }
}
